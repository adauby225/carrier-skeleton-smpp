plugins {
	id 'java'
	id 'jacoco'
	id 'java-library'
	
}

group = 'com.carrier.smpp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'


repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

sourceSets{
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}
configurations{
	intTestImplementation.extendsFrom implementation
}

dependencies {
	implementation group: 'org.apache.logging.log4j', name: 'log4j', version: '2.12.1', ext: 'pom'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
	implementation 'com.github.Adauby:cloudhopper-smpp:04a3959'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
	
	testImplementation group: 'junit', name: 'junit', version: '4.12'
	intTestImplementation group: 'junit', name: 'junit', version: '4.12'
	
}

test{
	reports.html.setDestination(file("$reports.html.destination/unit"))
	reports.junitXml.setDestination(file("$reports.junitXml.destination/unit"))
}

jacocoTestReport{
    	executionData(test)
}

task integTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'
		
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test
}
	
	tasks.withType(Test) {
    	testLogging {
        	showStandardStreams = true

    	}
    }
    
    check.dependsOn integTest
